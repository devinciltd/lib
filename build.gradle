buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:1.3.0'
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.7'
    classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    classpath 'com.dicedmelon.gradle:jacoco-android:0.1.0'
  }
}

plugins {
  id 'pl.allegro.tech.build.axion-release' version '1.3.1'
  id 'me.tatarka.retrolambda' version '3.2.2'
}

ext {
  configCompileSdkVersion = 23
  configTargetSdkVersion = 23
  configBuildToolsVersion = '23.0.1'

  jacocoExcludes = ['**/*_MembersInjector.class',
                    '**/Dagger*Component.class',
                    '**/Dagger*Component$Builder.class',
                    '**/*Module_*Factory.class',
                    '**/R.class',
                    '**/R$*.class',
                    '**/*$ViewInjector*.*',
                    '**/BuildConfig.*',
                    '**/Manifest*.*']

  fixMissingResourceClasses = 'gradle/fixMissingResourceClasses.gradle'

  supportVersion = '23.0.1'

  support = [appCompat  : "com.android.support:appcompat-v7:$supportVersion",
             annotations: "com.android.support:support-annotations:$supportVersion",
             design     : "com.android.support:design:$supportVersion"]

  crashlytics = 'com.crashlytics.sdk.android:crashlytics:2.5.1@aar'
  dagger = [dagger  : 'com.google.dagger:dagger:2.0.1',
            compiler: 'com.google.dagger:dagger-compiler:2.0.1']
  jsr250 = 'javax.annotation:jsr250-api:1.0'

  assertJAndroidVersion = '1.1.0'
  assertJAndroid = [core  : "com.squareup.assertj:assertj-android:$assertJAndroidVersion",
                    design: "com.squareup.assertj:assertj-android-design:$assertJAndroidVersion@aar"]
  assertJCore = 'org.assertj:assertj-core:2.2.0'
  junit = 'junit:junit:4.12'
  mockitoCore = 'org.mockito:mockito-core:1.10.19'
  robolectric = [core      : 'org.robolectric:robolectric:3.0',
                 androidAll: 'org.robolectric:android-all:5.1.1_r9-robolectric-1']

  espressoCore = 'com.android.support.test.espresso:espresso-core:2.2'
}

allprojects {
  repositories {
    jcenter()
    maven { url System.getenv('ANDROID_HOME') + '/extras/android/m2repository' }
  }

  project.version = scmVersion.version
}
